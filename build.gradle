plugins {
  id 'java'
  id 'org.springframework.boot' version '2.2.6.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'com.google.cloud.tools.jib' version '2.2.0'
}

group 'net.rmelick'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
  mavenCentral()
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter')
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework:spring-websocket'
  implementation 'org.springframework:spring-messaging'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation group: 'org.glassfish.tyrus', name: 'tyrus-client', version: '1.16'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
  implementation group: 'commons-codec', name: 'commons-codec', version: '1.11'
  implementation(group: 'com.fossgalaxy.games', name: 'fireworks', version: '0.2.4') {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
  }
  runtimeOnly group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.16'
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation('org.springframework.boot:spring-boot-starter-test')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.1'
  testImplementation group: 'junit', name: 'junit', version: '4.12'
}


jib {
  from {
    // latest 11 image as of April 25 2020
    image = "gcr.io/distroless/java@sha256:c94feda039172152495b5cd60a350a03162fce4f8986b560ea555de4d276ce19"
  }
  to {
    image = "gcr.io/${System.getenv('RUN_PROJECT_ID')}/${System.getenv('RUN_SERVICE_NAME')}:${System.getenv('GITHUB_SHA')}"
    def localCredsFile = System.getenv('RUN_SERVICE_ACCOUNT_KEY')
    if (localCredsFile != null) {
      auth {
        username = '_json_key'
        password = file(localCredsFile).text
      }
    }

  }
}